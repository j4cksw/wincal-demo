*** Settings ***
Library  AutoItLibrary
Test Setup  Start Calculator
Test Teardown  Stop Calculator

*** Test case ****
Rule For Each Number Button
	[Template]  Click On Number Should Show Correct Number
	#--------------------#
    # NUMBER | BUTTON_ID #
    #--------------------#
    	1	Button44
    	2	Button49
    	3	Button54
        4	Button43
        5	Button48
        6	Button53
        7	Button42
        8	Button47
        9	Button52

*** Keyword ***
Start Calculator
	Run  calc.exe
	Wait For Active Window  Calculator

Stop Calculator
	Win Close  Calculator  ${EMPTY}

Click On Number Should Show Correct Number
	[Arguments]  ${NUMBER}  ${BUTTON_ID}
	Clear Answer
	Click Button  ${BUTTON_ID}
	Answer Should Be  ${NUMBER}

Clear Answer
	Click Button  Button73
	
Click Button
	[Arguments]  ${BUTTON_ID}
	Control Click  Calculator  ${EMPTY}  ${BUTTON_ID}

Answer Should Be
	[Arguments]  ${EXPECTED_ANSWER}
	${ANSWER_TEXT}=  Control Get Text  Calculator  ${EMPTY}  Edit1
	Should Be Equal  ${EXPECTED_ANSWER}.  ${ANSWER_TEXT.strip()}